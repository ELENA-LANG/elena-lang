import system'routines.
import extensions.
import extensions'routines.

// --- Joinable --

joinable = (:aFormer:aLater)(aFormer[aFormer length - 1] == aLater[0]).

dispatcher =
{
    eval(object anArray, Func2 aFunction)
    [
        ^ aFunction(anArray[0],anArray[1]).
    ]

    eval(object anArray, Func3 aFunction)
    [
        ^ aFunction(anArray[0], anArray[1],anArray[2]).
    ]
    
    eval(object anArray, Func4 aFunction)
    [
        ^ aFunction(anArray[0],anArray[1],anArray[2],anArray[3]).
    ]
    
    eval(object anArray, Func5 aFunction)
    [
        ^ aFunction(anArray[0],anArray[1],anArray[2],anArray[3],anArray[4]).
    ]

}.

// --- AmbValueCollection ---

class AmbValueCollection
{
    object theCombinator.
    
    constructor new(V<object> Arguments)
    [
        theCombinator := SequentialEnumerator new(Arguments).
    ]

    seek : aCondition
    [
        theCombinator reset.

        theCombinator seekEach(:v)(dispatcher eval(v,aCondition))
    ]
    
    do : aFunction
    [
        var aResult := theCombinator get.
        if (nil != aResult)
            [ dispatcher eval(aResult,aFunction) ];
            [ InvalidArgumentException new; raise ]
    ]
}

// --- ambOperator ---

ambOperator =
{
    generic for(V<object> Arguments)
        = AmbValueCollection new(Arguments).
}.

// --- Program ---

public program1 =
[
    try(ambOperator for((1,2,4),(4,5,6)); seek(:a:b)(a * b == 8); do(:a:b)
        [ console printLine(a," * ",b," = 8") ])
    {
        on(Exception e)
        [
            console printLine:"AMB is angry"
        ]
    }.
        
    console readChar.        
].

public program2 =
[
    try(ambOperator 
        for(("the","that","a"),("frog", "elephant", "thing"),("walked", "treaded", "grows"),("slowly", "quickly"));
        seek(:a:b:c:d) ( joinable(a,b) && joinable(b,c) && joinable(c,d) );
        do(:a:b:c:d) [ console printLine(a," ",b," ",c," ",d) ])
    {
        on(Exception e)
        [
            console printLine:"AMB is angry"
        ]        
    }.
        
    console readChar.
].
