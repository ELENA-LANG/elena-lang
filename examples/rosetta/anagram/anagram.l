// Two or more words can be composed of the same characters, but in 
// a different order. Using the word list at 
// http://www.puzzlers.org/pub/wordlists/unixdict.txt, find the sets of words 
// that share the same characters that contain the most words in them. 

import system'routines;
import system'calendar;
import system'io;
import system'collections;
import extensions;
import extensions'routines;
import extensions'text;

// --- Normalized ---

extension op
{
     string normalized()
         = self.toArray().ascendant().summarize(new StringWriter());
}

// --- Program ---

public program()
{
    var start := now;
    
    auto dictionary := new Map<string,object>();

    File.assign("unixdict.txt").forEachLine:(word)
    {
        var key := word.normalized();
        var item := dictionary[key];
        if (nil == item)
        {
            item := new ArrayList();
            dictionary[key] := item
        };
    
        item.append:word
    };

    dictionary.Values
        .sort:(former,later => former.Item2.Length > later.Item2.Length )
        .top:20
        .forEach:(pair){ console.printLine(pair.Item2) };
    
    var end := now;
   
    var diff := end - start;

    console.printLine("Time elapsed in msec:",diff.Milliseconds);
    
    console.readChar()
}