__define START          	1
__define eps            	2

__define SUBJECT_ARG		4112
__define EXPRESSION		4114
__define TOKEN_PARAM		4117
__define PARAMETER		4119
__define NESTED_CLASS		4120
__define OBJECT			4121
__define CODE_BLOCK		4122
__define MESSAGE		4123
__define MESSAGE_PARAMETER	4124
__define BLOCK_END		4125
__define L4_OPERATION           4127
__define DISPATCH               4128
__define ASSIGNING		4129
__define BASE_CLASS             4131
__define L3_OPERATION           4133
__define L7_OPERATION           4134
__define RET_STATEMENT		4135
__define L5_OPERATION           4136
__define EXTENSION		4139
__define RESEND                 4145
__define MESSAGE_REFERENCE	4147
__define ROOT_EXPRESSION        4152
__define L6_OPERATION           4161
__define SIZE_PARAM             4162
__define TOKEN   		4172
__define SCOPE   		4173
__define ATTRIBUTE   		4174
__define ANGLE_OPERATOR         4177
__define ANGLE_OBJECT           4178
__define ARRAY_OPERATION        4179
__define ROOT_ANGLE_OPERATOR    4180
__define ROOT_ANGLE_OBJECT      4181
__define ROOT_L6_OPERATION      4182
__define TEMPLATE_OPERATOR      4183

__define DECLARATION_END_EXPECTED	16384
__define STATEMENT_END_EXPECTED         16385
__define BLOCK_CLOSING_EXPECTED         16386

__define eof            	12291
__define literal		12292
__define identifier		12293
__define private	        12294
__define reference		12295
__define integer		12296
__define hex			12297
__define real			12298
__define character		12299
__define long                   12300
__define wide                   12301
__define attribute		12303

START ->
	SCOPE_DECLARATION DECLARATIONS eof
	| eof

DECLARATIONS ->
	SCOPE_DECLARATION DECLARATIONS
	| eps
	
SCOPES ->
	TOKEN TOKENS SCOPE SCOPES
	| eps

TOKENS ->
	TOKEN TOKENS
	| eps

TOKEN_PARAMS ->
	, TOKEN_PARAM TOKEN_PARAMS
	| )

SCOPE_DECLARATION ->
	TOKEN TOKENS SCOPE

SCOPE ->
	= TOKEN_DECLARATION DECLARATION_END
	| { SCOPES CLOSING_CURLY_BRACKET
	| => DISPATCH DECLARATION_END
	| <= RESEND
	| CODE_BLOCK
	| : PARAMETER PARAMETER_BODY METHOD_BODY
	| ( BRACKET_BODY 
	| DECLARATION_END
	| :: BASE_CLASS NEXT_BASE_CLASS SCOPE_BODY

NEXT_BASE_CLASS ->
	, BASE_CLASS NEXT_BASE_CLASS
	| eps

SCOPE_BODY ->
	CLASS_BODY
	| = SYMBOL_EXPRESSION DECLARATION_END

CLASS_BODY ->
	{ SCOPES CLOSING_CURLY_BRACKET

BRACKET_BODY ->
	SIZE_PARAM CLOSING_BRACKET DECLARATION_END
	| SIGNATURE_BODY METHOD_BODY

TOKEN_DECLARATION ->
	SYMBOL_EXPRESSION
	| ATTRIBUTE

PARAMETER_BODY ->
	: PARAMETER NEXT_PARAMETER
	| SUBJECT_ARG : PARAMETER NEXT_SUBJECT_PARAMETER
	| eps
	        
NEXT_SUBJECT_PARAMETER ->
	SUBJECT_ARG : PARAMETER NEXT_SUBJECT_PARAMETER
	| eps

NEXT_PARAMETER ->
	: PARAMETER NEXT_PARAMETER
	| eps

SIGNATURE_BODY ->
	TOKEN_PARAM PARAMETER NEXT_SIGNATURE

NEXT_SIGNATURE ->
	, TOKEN_PARAM PARAMETER NEXT_SIGNATURE
	| CLOSING_BRACKET

METHOD_BODY ->
	CODE_BLOCK
	| = RET_STATEMENT DECLARATION_END
	| => DISPATCH DECLARATION_END
	| <= RESEND

DISPATCH ->
	OBJECT

RESEND ->
	MESSAGE RESEND_BODY

RESEND_BODY ->
	; CODE_BLOCK
	|  DECLARATION_END

SYMBOL_EXPRESSION ->
	EXPRESSION

NESTED_CLASS ->
	{ SCOPES CLOSING_CURLY_BRACKET

CODE_BLOCK ->
	[ STATEMENTS

STATEMENTS ->
	ROOT_EXPRESSION NEXT_STATEMENT
	| ^ RET_STATEMENT RET_STATEMENT_END
	| BLOCK_END

NEXT_STATEMENT ->
	STATEMENT_END STATEMENTS
	| BLOCK_END	

RET_STATEMENT ->
	EXPRESSION

EXTENSION ->
	~ OBJECT

ASSIGNING ->
	:= EXPRESSION

SUB_EXPRESSION ->
	EXPRESSION NEXT_SUB_EXPRESSION

NEXT_SUB_EXPRESSION ->
	, EXPRESSION NEXT_SUB_EXPRESSION
	| CLOSING_BRACKET

ROOT_EXPRESSION ->
	identifier ROOT_OPERATIONS
	| private ROOT_OPERATIONS
	| reference OPERATIONS ROOT_BODY
	| literal OPERATIONS
	| wide OPERATIONS
	| integer OPERATIONS
	| hex OPERATIONS
	| long OPERATIONS
	| real OPERATIONS
	| character OPERATIONS
	| % MESSAGE_REFERENCE OPERATIONS
	| CODE_BLOCK OPERATIONS
	| NESTED_CLASS OPERATIONS
	| ( SUB_EXPRESSION OPERATIONS

ROOT_OPERATIONS ->
	MESSAGE NEXT_MESSAGE L3_OPERATIONS L4_OPERATIONS L5_OPERATIONS L6_OPERATIONS L7_OPERATIONS ROOT_BODY
	| ASSIGNING
	| :: OBJECT_EXT OPERATIONS ROOT_BODY
	| ( ROOT_SUB_EXPRESSION ROOT_CODE_BODY
	| L5_OPERATION L5_OPERATIONS L6_OPERATIONS
	| ROOT_L6_OPERATION
	| eps

ROOT_CODE_BODY ->
	CODE_BLOCK ROOT_CODE_BODY_OP
	| TEMPLATE_OPERATOR
	| eps	

ROOT_CODE_BODY_OP ->
        ; ROOT_CODE_BODY2
	| eps

ROOT_CODE_BODY2 ->
	CODE_BLOCK

ROOT_L6_OPERATION ->
	< ROOT_ANGLE_OBJECT

ROOT_ANGLE_OBJECT ->
	identifier ROOT_ANGLE_OPERATION

ROOT_ANGLE_OPERATION ->
	> ROOT_ANGLE_OPERATOR

ROOT_ANGLE_OPERATOR ->
	identifier ASSIGNING
	| eps

TEMPLATE_OPERATOR ->
	identifier ASSIGNING

ROOT_BODY ->
	ASSIGNING
	| eps

EXPRESSION ->
	OBJECT OPERATIONS

MESSAGE_PARAMETER ->
	OBJECT

OBJECT ->
	identifier OBJECT_BODY
	| private
	| reference
	| literal
	| wide
	| integer
	| hex
	| long
	| real
	| character
	| % MESSAGE_REFERENCE
	| CODE_BLOCK	
	| NESTED_CLASS
	| ( SUB_EXPRESSION

OBJECT_BODY ->
	:: OBJECT_EXT
	| ( ROOT_SUB_EXPRESSION
	| eps

ROOT_SUB_EXPRESSION ->
	EXPRESSION CLOSING_BRACKET

OBJECT_EXT ->
	NESTED_CLASS

OPERATIONS ->
	MESSAGE NEXT_MESSAGE L3_OPERATIONS L4_OPERATIONS L5_OPERATIONS L6_OPERATIONS L7_OPERATIONS
	| EXTENSION MESSAGE NEXT_MESSAGE
	| L3_OPERATION L3_OPERATIONS L4_OPERATIONS L5_OPERATIONS L6_OPERATIONS
	| L4_OPERATION L4_OPERATIONS L5_OPERATIONS L6_OPERATIONS
	| L5_OPERATION L5_OPERATIONS L6_OPERATIONS
        | L6_OPERATION
	| ARRAY_OPERATION
	| L7_OPERATION
	| eps

MESSAGE_OPERATIONS ->
	MESSAGE NEXT_MESSAGE
	| EXTENSION MESSAGE NEXT_MESSAGE
	| eps

MESSAGE ->
	identifier MESSAGE_BODY
	| private MESSAGE_BODY

MESSAGE_BODY ->
	SUBJECT_ARG AFTER_FIRST_SUBJECT
	| : MESSAGE_PARAMETER NEXT_MESSAGE_PARAMETER
	| ( MESSAGE_LIST
	| eps

AFTER_FIRST_SUBJECT ->
	: MESSAGE_PARAMETER NEXT_MESSAGE_PARAMETER
	| eps

NEXT_MESSAGE_PARAMETER ->
	: MESSAGE_PARAMETER MESSAGE_PARAMETERS
	| SUBJECT_ARG : MESSAGE_PARAMETER SUBJ_MESSAGE_PARAMETERS
	| eps

MESSAGE_PARAMETERS ->
	: MESSAGE_PARAMETER MESSAGE_PARAMETERS
	| eps

SUBJ_MESSAGE_PARAMETERS ->
	SUBJECT_ARG : MESSAGE_PARAMETER SUBJ_MESSAGE_PARAMETERS
	| eps

MESSAGE_LIST ->
	EXPRESSION NEXT_MESSAGE_LIST_ITEM

NEXT_MESSAGE_LIST_ITEM ->
	, EXPRESSION NEXT_MESSAGE_LIST_ITEM
	| CLOSING_BRACKET
	      
NEXT_MESSAGE ->
	; MESSAGE NEXT_MESSAGE
	| eps

L3_OPERATION ->
	* L3_OPERAND
	| / L3_OPERAND
	| && L3_OPERAND
	| ^^ L3_OPERAND

L3_OPERATIONS ->
	L3_OPERATION L3_OPERATIONS
	| eps

L3_OPERAND ->
	OBJECT MESSAGE_OPERATIONS

L4_OPERATION ->
	+ L4_OPERAND
	| - L4_OPERAND
	| &| L4_OPERAND

L4_OPERATIONS ->
	L4_OPERATION L4_OPERATIONS
	| eps

L4_OPERAND ->
	OBJECT MESSAGE_OPERATIONS L3_OPERATIONS

L5_OPERATION ->
	<< L5_OPERAND
	| >> L5_OPERAND

L5_OPERATIONS ->
	L5_OPERATION L5_OPERATIONS
	| eps	

L5_OPERAND ->
	OBJECT MESSAGE_OPERATIONS L3_OPERATIONS L4_OPERATIONS

L6_OPERATION ->
	== L6_OPERAND
	| != L6_OPERAND
	| < ANGLE_OBJECT
	| > L6_OPERAND
	| <= L6_OPERAND
	| >= L6_OPERAND

L6_OPERATIONS ->
	L6_OPERATION
	| eps	

L6_OPERAND ->
	OBJECT MESSAGE_OPERATIONS L3_OPERATIONS L4_OPERATIONS L5_OPERATIONS

ANGLE_OBJECT ->
	identifier ANGLE_OPERATION
	| private ANGLE_OPERATION
	| reference MESSAGE_OPERATIONS L3_OPERATIONS L4_OPERATIONS L5_OPERATIONS
	| literal MESSAGE_OPERATIONS L3_OPERATIONS L4_OPERATIONS L5_OPERATIONS
	| wide MESSAGE_OPERATIONS L3_OPERATIONS L4_OPERATIONS L5_OPERATIONS
	| integer MESSAGE_OPERATIONS L3_OPERATIONS L4_OPERATIONS L5_OPERATIONS
	| % MESSAGE_REFERENCE MESSAGE_OPERATIONS L3_OPERATIONS L4_OPERATIONS L5_OPERATIONS
	| CODE_BLOCK MESSAGE_OPERATIONS L3_OPERATIONS L4_OPERATIONS L5_OPERATIONS
	| NESTED_CLASS MESSAGE_OPERATIONS L3_OPERATIONS L4_OPERATIONS L5_OPERATIONS

ANGLE_OPERATION ->
	MESSAGE NEXT_MESSAGE L3_OPERATIONS L4_OPERATIONS L5_OPERATIONS
	| EXTENSION MESSAGE NEXT_MESSAGE L3_OPERATIONS L4_OPERATIONS L5_OPERATIONS
	| L3_OPERATION L3_OPERATIONS L4_OPERATIONS L5_OPERATIONS
	| L4_OPERATION L4_OPERATIONS L5_OPERATIONS
	| L5_OPERATION L5_OPERATIONS
	| ANGLE_OPERATOR
	| eps
	
ANGLE_OPERATOR ->
	>

L7_OPERATION ->
	? L7_OPERAND ELSE_BODY
	| ELSE_OPERATION

L7_OPERATIONS ->
	L7_OPERATION
	| eps
	
ELSE_BODY ->
	ELSE_OPERATION
	| eps	

ELSE_OPERATION ->
	! L7_OPERAND

L7_OPERAND ->
	CODE_BLOCK

ARRAY_OPERATION ->
	[ EXPRESSION CLOSING_SBRACKET
	| @ OBJECT
	
DECLARATION_END ->
	TERMINATOR
	| DECLARATION_END_EXPECTED

TOKEN ->
	identifier
	| private 

ATTRIBUTE ->
	attribute

SUBJECT_ARG ->
	identifier

PARAMETER ->
	identifier

TOKEN_PARAM ->
	identifier
	| reference

MESSAGE_REFERENCE ->
	identifier
	| literal

SIZE_PARAM ->
	integer
	
BASE_CLASS ->
	identifier TOKEN_BODY
	| reference 

TOKEN_BODY ->
	( TOKEN_PARAM TOKEN_PARAMS
	| eps	

TERMINATOR ->
	.

CLOSING_BRACKET ->
	)

CLOSING_SBRACKET ->
	]

STATEMENT_END ->
	TERMINATOR
	| STATEMENT_END_EXPECTED

BLOCK_END ->
	]
	| BLOCK_CLOSING_EXPECTED

RET_STATEMENT_END ->
	TERMINATOR BLOCK_END
	| BLOCK_END

CLOSING_CURLY_BRACKET ->
	}
