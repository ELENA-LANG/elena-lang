__define START          	1
__define eps            	2

__define TOKEN			4112
__define ROOT_EXPRESSION        4113
__define EXPRESSION             4114
__define DECLARATION            4115
__define PARAMETER		4119
__define NESTED_CLASS		4120
__define BLOCK		        4122
__define MESSAGE		4123
__define BLOCK_END		4125
__define DISPATCH               4128
__define ASSIGN_OPERATOR	4129
__define BASE                   4131
__define ATTRIBUTE_DECLARATION  4174
__define L4_OPERATOR            4196
__define RET_EXPRESSION         4198
__define IMPLICIT_MESSAGE       4199
__define SIZE			4200
__define DYNAMIC_SIZE		4201

__define eof            	12291
__define literal		12292
__define identifier		12293
__define reference		12295
__define integer		12296
__define hex			12297
__define attribute		12303

START ->
	ATTRIBUTE_DECLARATION ATTRIBUTE_DECLARATIONS DECLARATIONS eof
	| TOKEN TOKENS DECLARATION DECLARATIONS eof
	| eof

ATTRIBUTE_DECLARATIONS ->
	ATTRIBUTE_DECLARATION ATTRIBUTE_DECLARATIONS
	| eps

DECLARATIONS ->
	TOKEN TOKENS DECLARATION DECLARATIONS
	| eps

DECLARATION ->
	SYMBOL_DECLARATION DECLARATION_END
	| CLASS_DECLARATION
	| COLON BASE CLASS_DECLARATION
	| METHOD_DECLARATION
	| DECLARATION_END
	| SIZE_DECLARATION DECLARATION_END

CLASS_DECLARATION ->
	{ DECLARATIONS }

SYMBOL_DECLARATION ->
	= EXPRESSION

METHOD_DECLARATION ->
	( PARAMETERS ) METHOD_BODY

PARAMETERS ->
	PARAMETER NEXT_PARAMETER
	| eps

NEXT_PARAMETER ->
	, PARAMETER NEXT_PARAMETER
	| eps

PARAMETER ->
	TOKEN TOKENS

METHOD_BODY ->
	BLOCK
	| => DISPATCH DECLARATION_END
	| = RET_EXPRESSION DECLARATION_END

RET_EXPRESSION ->
	EXPRESSION

BLOCK ->
	OPENING_CURLY_BRACKET STATEMENTS

STATEMENTS ->
	STATEMENT NEXT_STATEMENT
	| ^ RET_EXPRESSION RET_STATEMENT_END
	| BLOCK_END

RET_STATEMENT_END ->
	TERMINATOR BLOCK_END
	| BLOCK_END

NEXT_STATEMENT ->
        DECLARATION_END STATEMENTS
	| BLOCK_END

DISPATCH ->
	EXPRESSION

STATEMENT ->
	ROOT_EXPRESSION

ROOT_EXPRESSION ->
	TOKEN TOKENS TOKEN_OPERATIONS ROOT_OPERATIONS
	| NESTED_CLASS OPERATIONS ROOT_OPERATIONS
	| integer OPERATIONS ROOT_OPERATIONS
	| hex OPERATIONS ROOT_OPERATIONS
	| literal OPERATIONS ROOT_OPERATIONS
	| OPENING_BRACKET SUB_BODY OPERATIONS ROOT_OPERATIONS

EXPRESSION ->
	identifier TOKENS TOKEN_OPERATIONS
	| reference TOKEN_OPERATIONS
	| integer OPERATIONS
	| hex OPERATIONS
	| literal OPERATIONS
	| NESTED_CLASS OPERATIONS
	| OPENING_BRACKET SUB_EXPRESSION OPERATIONS

SUB_EXPRESSION ->
	CLOSING_BRACKET CLOSURE
	| identifier TOKENS SUB_TOKEN_OPERATIONS
	| NESTED_CLASS OPERATIONS CLOSING_BRACKET
	| integer OPERATIONS CLOSING_BRACKET
	| hex OPERATIONS CLOSING_BRACKET
	| literal OPERATIONS CLOSING_BRACKET
	| ( SUB_EXPRESSION OPERATIONS CLOSING_BRACKET

OPERATION ->
	. MESSAGE MESSAGE_BODY
	| IMPLICIT_OPERATION
	| L1_OPERATION
	| L2_OPERATION
	| ARRAY_OPERATION

SUB_TOKENS ->
	, TOKEN TOKENS SUB_TOKENS

SUB_TOKEN_OPERATIONS ->
	SUB_TOKENS ) CLOSURE_OPTION
	| ) CLOSURE_OPTION

TOKEN_OPERATIONS ->
	OPERATION TOKEN_OPERATIONS
	| ASSIGN_OPERATOR EXPRESSION
	| eps

OPERATIONS ->
	OPERATION OPERATIONS
	| L3_OPERATION OPERATIONS
	| ASSIGN_OPERATOR EXPRESSION
	| eps

ROOT_OPERATIONS ->
	L4_OPERATION
	| eps

ARRAY_OPERATIONS ->
	[ ARRAY_OPERATION ARRAY_OPERATIONS
	| eps

ARRAY_OPERATION ->
	[ EXPRESSION CLOSING_SBRACKET

L1_OPERATIONS ->
	L1_OPERATION L1_OPERATIONS
	| eps

L1_OPERATION ->
	* L1_OPERAND
	| / L1_OPERAND 

L1_OPERAND ->
	OBJECT

L2_OPERATIONS ->
	L2_OPERATION L2_OPERATIONS
	| eps

L2_OPERATION ->
	+ L2_OPERAND
	| - L2_OPERAND 

L2_OPERAND ->
	OBJECT

L3_OPERATIONS ->
	L3_OPERATION L3_OPERATIONS
	| eps

L3_OPERATION ->
	< L3_OPERAND
	| > L3_OPERAND 

L3_OPERAND ->
	OBJECT

L4_OPERATION ->
	L4_OPERATOR L4_OPERAND

L4_OPERATOR ->
	?

L4_OPERAND ->
	BLOCK

IMPLICIT_OPERATION ->
	IMPLICIT_MESSAGE MESSAGE_PARAMETERS )

NESTED_CLASS ->
	OPENING_CURLY_BRACKET DECLARATIONS CLOSING_CURLY_BRACKET

TOKENS ->
	TOKEN TOKENS
	| eps

CLOSURE_OPTION ->
	CLOSURE
	| eps

CLOSURE ->
	BLOCK

MESSAGE ->
	identifier

MESSAGE_BODY ->
	( MESSAGE_PARAMETERS )

MESSAGE_PARAMETERS ->
	EXPRESSION NEXT_MESSAGE_PARAMETER
	| eps

NEXT_MESSAGE_PARAMETER ->
	, EXPRESSION NEXT_MESSAGE_PARAMETER
	| eps

BASE ->
	TOKEN

TOKEN_BODY ->
	< TOKEN NEXT_TOKEN_PARAM
	| DYNAMIC_SIZE
	| eps

NEXT_TOKEN_PARAM ->
	, TOKEN NEXT_TOKEN_PARAM
	| > DYNAMIC_SIZE_OPTION

DYNAMIC_SIZE_OPTION ->
	DYNAMIC_SIZE
	| eps

TOKEN ->
	identifier TOKEN_BODY

ATTRIBUTE_DECLARATION ->
	attribute ATTRIBUTE_BODY

ATTRIBUTE_BODY ->
	= TOKEN DECLARATION_END

DYNAMIC_SIZE ->
	[]

SIZE_DECLARATION ->
	[ SIZE ]

SIZE ->
	integer

DECLARATION_END ->
	;

OPENING_BRACKET ->
	(

OPENING_CURLY_BRACKET ->
	{

BLOCK_END ->
	}

CLOSING_CURLY_BRACKET ->
	}

IMPLICIT_MESSAGE ->
	(

ASSIGN_OPERATOR ->
	:=

COLON ->
	:

CLOSING_SBRACKET ->
	]

CLOSING_BRACKET ->
	)

