__define START          	1
__define eps            	2

__define SUBJECT_ARG		4112
__define EXPRESSION		4114
__define TOKEN_PARAM		4117
__define PARAMETER		4119
__define NESTED_CLASS		4120
__define OBJECT			4121
__define CODE_BLOCK		4122
__define MESSAGE		4123
__define MESSAGE_PARAMETER	4124
__define BLOCK_END		4125
__define L4_OPERATION           4127
__define DISPATCH               4128
__define ASSIGNING		4129
__define BASE_CLASS             4131
__define L3_OPERATION           4133
__define L7_OPERATION           4134
__define RET_STATEMENT		4135
__define L5_OPERATION           4136
__define ALT_MESSAGE		4140
__define RESEND                 4145
__define ROOT_EXPRESSION        4152
__define L6_OPERATION           4161
__define SIZE_PARAM             4162
__define TOKEN   		4172
__define SCOPE   		4173
__define ATTRIBUTE   		4174
__define ANGLE_OPERATOR         4177
__define L8_OPERATION           4188
__define IDLE_MESSAGE_PARAMETER 4189
__define SUB_MESSAGE_PARAMETER  4190

__define DECLARATION_END_EXPECTED	16384
__define STATEMENT_END_EXPECTED         16385
__define BLOCK_CLOSING_EXPECTED         16386

__define eof            	12291
__define literal		12292
__define identifier		12293
__define private	        12294
__define reference		12295
__define integer		12296
__define hex			12297
__define real			12298
__define character		12299
__define long                   12300
__define wide                   12301
__define constant               12302
__define attribute		12303
__define member			12304

START ->
	SCOPE_DECLARATION DECLARATIONS eof
	| eof

DECLARATIONS ->
	SCOPE_DECLARATION DECLARATIONS
	| eps
	
SCOPE_DECLARATION ->
	TOKEN TOKENS SCOPE

TOKEN_DECLARATION ->
	= TOKEN_DECLARATION_BODY

TOKEN_DECLARATION_BODY ->
	SYMBOL_EXPRESSION
	| ATTRIBUTE

SCOPES ->
	TOKEN TOKENS SCOPE SCOPES
	| eps

SCOPE ->
	TOKEN_DECLARATION DECLARATION_END
	| DECLARATION_END
	| OPENING_CURLY_BRACKET SCOPES CLOSING_CURLY_BRACKET
	| DOUBLE_COLON BASE_CLASS NEXT_BASE_CLASS CLASS_SCOPE
	| CODE_BLOCK
	| DISPATCH_DECLARATION DECLARATION_END
	| COLON PARAMETER PARAMETER_BODY METHOD_BODY
	| OPENING_BRACKET SIGNATURE_BODY METHOD_BODY
	| RESEND_OPERATOR RESEND

CLASS_SCOPE ->
	{ SCOPES CLOSING_CURLY_BRACKET
	| DECLARATION_END
	| CODE_BLOCK

PARAMETER_BODY ->
	: PARAMETER NEXT_PARAMETER
	| SUBJECT_ARG : PARAMETER NEXT_SUBJECT_PARAMETER
	| eps

NEXT_BASE_CLASS ->
	, BASE_CLASS NEXT_BASE_CLASS
	| eps
	        
NEXT_SUBJECT_PARAMETER ->
	SUBJECT_ARG : PARAMETER NEXT_SUBJECT_PARAMETER
	| eps

NEXT_PARAMETER ->
	: PARAMETER NEXT_PARAMETER
	| eps
	
SIGNATURE_BODY ->
	TOKEN_PARAM PARAMETER NEXT_SIGNATURE

NEXT_SIGNATURE ->
	, TOKEN_PARAM PARAMETER NEXT_SIGNATURE
	| CLOSING_BRACKET

METHOD_BODY ->
	CODE_BLOCK
	| = RET_STATEMENT DECLARATION_END
	| => DISPATCH DECLARATION_END
	| <= RESEND

DISPATCH_DECLARATION ->
	=> DISPATCH 
	
CODE_BLOCK ->
	OPEN_SBRACKET STATEMENTS

DISPATCH ->
	OBJECT

RESEND ->
	MESSAGE RESEND_BODY

RESEND_BODY ->
	DECLARATION_END

STATEMENTS ->
	ROOT_EXPRESSION NEXT_STATEMENT
	| ^ RET_STATEMENT RET_STATEMENT_END
	| BLOCK_END

NEXT_STATEMENT ->
	STATEMENT_END STATEMENTS
	| BLOCK_END	

RET_STATEMENT ->
	EXPRESSION

SYMBOL_EXPRESSION ->
	EXPRESSION

ROOT_EXPRESSION ->
	identifier ROOT_OPERATIONS
	| private ROOT_OPERATIONS
	| reference ROOT_OPERATIONS
	| OPENING_BRACKET SUB_EXPRESSION ROOT_SUB_OPERATIONS

SUB_EXPRESSION ->
	EXPRESSION CLOSING_BRACKET
	| : PARAMETER CLOSURE_ARGS

CLOSURE_ARGS ->
	: PARAMETER CLOSURE_ARGS
	| ) CLOSURE_ARGS_BODY

CLOSURE_ARGS_BODY ->
	CODE_BLOCK
	| ( RET_STATEMENT )

EXPRESSION ->
        OBJECT OPERATIONS

OBJECT ->
	identifier OBJECT_BODY
	| private
	| reference OBJECT_BODY
	| integer
	| hex
	| long
	| real
	| literal
	| wide
	| character
	| CODE_BLOCK	
	| OPENING_BRACKET SUB_EXPRESSION
	| NESTED_CLASS

OBJECT_BODY ->
	:: OBJECT_EXT
	| eps

OBJECT_EXT ->
	NESTED_CLASS

ROOT_SUB_OPERATIONS ->
	L7_OPERATION
	| OPERATIONS

ROOT_OPERATIONS ->
	MESSAGE NEXT_MESSAGE L6_OPERATIONS ROOT_MESSAGE_OPERATIONS
	| ( SUB_BODY ROOT_BODY
	| L3_OPERATION
	| L4_OPERATION
	| L5_OPERATION
        | L6_OPERATION
	| L7_OPERATION
	| L8_OPERATION
	| := ASSIGNING
	| eps

ROOT_MESSAGE_OPERATIONS ->
	:= ASSIGNING
	| \ ALT_MESSAGE
	| L7_OPERATION
	| eps

ROOT_BODY ->
	CODE_BLOCK
	| eps

OPERATIONS ->
	MESSAGE NEXT_MESSAGE L6_OPERATIONS ALT_OPERATIONS
	| ( SUB_BODY
	| L3_OPERATION
	| L4_OPERATION
	| L5_OPERATION
        | L6_OPERATION
	| eps

SUB_BODY ->
	SUB_MESSAGE_PARAMETER SUB_MESSAGE_PARAMETERS
	| IDLE_MESSAGE_PARAMETER

IDLE_MESSAGE_PARAMETER ->
	CLOSING_BRACKET

SUB_MESSAGE_PARAMETER ->
	OBJECT OPERATIONS

SUB_MESSAGE_PARAMETERS ->
	, SUB_MESSAGE_PARAMETER SUB_MESSAGE_PARAMETERS
	| CLOSING_BRACKET

MESSAGE_OPERATIONS ->
	MESSAGE NEXT_MESSAGE
	| eps

NEXT_MESSAGE ->
	; MESSAGE NEXT_MESSAGE
	| eps

MESSAGE ->
	identifier MESSAGE_BODY
	| private MESSAGE_BODY

MESSAGE_BODY ->
	: MESSAGE_PARAMETER
	| SUBJECT_ARG SUBJECT_ARG_R
	| ( MESSAGE_LIST
	| eps

SUBJECT_ARG_R ->
	: MESSAGE_PARAMETER NEXT_SUBJECT_ARG

NEXT_SUBJECT_ARG ->
	SUBJECT_ARG SUBJECT_ARG_R
	| eps

MESSAGE_LIST ->
	EXPRESSION MESSAGE_LIST_R

MESSAGE_LIST_R  ->
	, EXPRESSION NEXT_MESSAGE_LIST_ITEM
	| CLOSING_BRACKET

NEXT_MESSAGE_LIST_ITEM ->
	, EXPRESSION NEXT_MESSAGE_LIST_ITEM
	| CLOSING_BRACKET

ALT_MESSAGE ->
	identifier MESSAGE_BODY

ASSIGNING ->
	EXPRESSION

NESTED_CLASS ->
	OPENING_CURLY_BRACKET SCOPES CLOSING_CURLY_BRACKET

MESSAGE_PARAMETER ->
	OBJECT

L3_OPERATION ->
	* L3_OPERAND
	| / L3_OPERAND
	| && L3_OPERAND
	| ^^ L3_OPERAND

L3_OPERAND ->
	OBJECT MESSAGE_OPERATIONS

L4_OPERATION ->
	+ L4_OPERAND
	| - L4_OPERAND
	| &| L4_OPERAND

L4_OPERAND ->
	OBJECT MESSAGE_OPERATIONS

L5_OPERATION ->
	<< L5_OPERAND
	| >> L5_OPERAND

L5_OPERAND ->
	OBJECT MESSAGE_OPERATIONS

L6_OPERATION ->
	< OBJECT TEMPLATE_BODY MESSAGE_OPERATIONS
	| > L6_OPERAND
	| == L6_OPERAND
	| != L6_OPERAND

L6_OPERATIONS ->
	L6_OPERATION
	| eps	

L6_OPERAND ->
	OBJECT MESSAGE_OPERATIONS

L7_OPERATION ->
	? L7_OPERAND ELSE_BODY
	| ! L7_OPERAND

ELSE_BODY ->
	ELSE_OPERATION
	| eps	

ELSE_OPERATION ->
	! L7_OPERAND

L7_OPERAND ->
	CODE_BLOCK

ALT_OPERATIONS ->
	\ ALT_MESSAGE
	| eps

L8_OPERATION ->
	+= L8_OPERAND	
	| -= L8_OPERAND
	| *= L8_OPERAND
	| /= L8_OPERAND

L8_OPERAND ->
	OBJECT

TEMPLATE_BODY ->
	ANGLE_OPERATOR TEMPLATE_OPERAND
	| eps

TEMPLATE_OPERAND ->
	( EXPRESSION )
	| eps

DECLARATION_END ->
	TERMINATOR
	| DECLARATION_END_EXPECTED

TOKEN_BODY ->
	< FIRST_TOKEN_PARAM
	| eps

FIRST_TOKEN_PARAM ->
	TOKEN_PARAM TOKEN_PARAMS
	| SIZE_PARAM CLOSING_ANGLE_BRACKET

TOKENS ->
	TOKEN TOKENS
	| eps

TOKEN ->
	identifier TOKEN_BODY
	| private 

TOKEN_PARAMS ->
	, TOKEN_PARAM TOKEN_PARAMS
	| SIZE_PARAM CLOSING_ANGLE_BRACKET
	| CLOSING_ANGLE_BRACKET

TOKEN_PARAM ->
	identifier
	| reference
	| private

SUBJECT_ARG ->
	identifier SUBJECT_BODY
	      
SUBJECT_BODY ->
	< TOKEN_PARAM >
	| eps

BASE_CLASS ->
	identifier TOKEN_BODY
	| reference 
	| private

ATTRIBUTE ->
	attribute

SIZE_PARAM ->
	integer
	
PARAMETER ->
	identifier

OPEN_SBRACKET ->
	[

CLOSING_CURLY_BRACKET ->
	}

CLOSING_ANGLE_BRACKET ->
	>

OPENING_CURLY_BRACKET ->
	{

OPENING_BRACKET ->
	(

CLOSING_BRACKET ->
	)

ANGLE_OPERATOR ->
	>

BLOCK_END ->
	]
	| BLOCK_CLOSING_EXPECTED

STATEMENT_END ->
	TERMINATOR
	| STATEMENT_END_EXPECTED

RET_STATEMENT_END ->
	TERMINATOR BLOCK_END
	| BLOCK_END

TERMINATOR ->
	.

COLON ->
	:

DOUBLE_COLON ->
	::

RESEND_OPERATOR ->
	<=