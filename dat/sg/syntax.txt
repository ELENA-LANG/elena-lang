__define START          	1
__define eps            	2

__define EXPRESSION		4114
__define TOKEN_PARAM		4117
__define PARAMETER		4119
__define OBJECT			4121
__define BLOCK		        4122
__define MESSAGE		4123
__define MESSAGE_PARAMETER	4124
__define BLOCK_END		4125
__define DISPATCH               4128
__define ASSIGNING		4129
__define BASE_CLASS             4131
__define RET_STATEMENT		4135
__define L4_OPERATION           4161
__define TOKEN   		4172
__define DECLARATION   		4173
__define ATTRIBUTE   		4174
__define IDLE_MESSAGE_PARAMETER 4189

__define eof            	12291
__define identifier		12293
__define private	        12294
__define reference		12295
__define attribute		12303
__define member			12304
__define global			12305

__define DECLARATION_END_EXPECTED	16384
__define STATEMENT_END_EXPECTED         16385
__define BLOCK_CLOSING_EXPECTED         16386

START ->
	TOKEN TOKENS DECLARATION DECLARATIONS eof
	| eof

DECLARATION ->
	EXPRESSION_DECLARATION DECLARATION_END
	| DISPATCH_DECLARATION DECLARATION_END	
	| CLASS_DECLARATION
	| DOUBLE_COLON BASE_CLASS NEXT_BASE_CLASS CLASS_DECLARATION
	| BLOCK
	| COLON PARAMETER METHOD_BODY
	| OPENING_BRACKET SIGNATURE_BODY METHOD_BODY
	| ASSIGN_OPERATOR ASSIGNING DECLARATION_END
	| DECLARATION_END
 
DECLARATIONS ->
	TOKEN TOKENS DECLARATION DECLARATIONS
	| eps

TOKENS ->
	TOKEN TOKENS
	| eps

NEXT_BASE_CLASS ->
	, BASE_CLASS NEXT_BASE_CLASS
	| eps
	        
CLASS_DECLARATION ->
	OPENING_CURLY_BRACKET DECLARATIONS CLOSING_CURLY_BRACKET

SIGNATURE_BODY ->
	TOKEN PARAMETER NEXT_SIGNATURE

NEXT_SIGNATURE ->
	, TOKEN_PARAM PARAMETER NEXT_SIGNATURE
	| CLOSING_BRACKET

METHOD_BODY ->
	BLOCK	
	| = EXPRESSION DECLARATION_END
	| => DISPATCH DECLARATION_END

DISPATCH_DECLARATION ->
	=> DISPATCH 

EXPRESSION_DECLARATION ->
	= EXPRESSION_BODY

EXPRESSION_BODY ->
	EXPRESSION
	| ATTRIBUTE

BLOCK ->
	OPEN_SBRACKET STATEMENTS

STATEMENTS ->
	^ RET_STATEMENT RET_STATEMENT_END
	| EXPRESSION NEXT_STATEMENT
	| BLOCK_END

NEXT_STATEMENT ->
	STATEMENT_END STATEMENTS
	| BLOCK_END

RET_STATEMENT ->
 	EXPRESSION

ASSIGNING ->
	EXPRESSION

SUB_EXPRESSION ->
	EXPRESSION CLOSING_BRACKET
	| IDLE_MESSAGE_PARAMETER

NEXT_EXPRESSION ->
	, OBJECT SUB_OPERATIONS NEXT_EXPRESSION
	| eps	

EXPRESSION ->
	OBJECT OPERATIONS

OPERATIONS ->
	MESSAGE MESSAGE_OPERATIONS L4_OPERATIONS NEXT_EXPRESSION LAST_OPERATION
	| L4_OPERATION L4_OPERATIONS NEXT_EXPRESSION LAST_OPERATION
	| , OBJECT SUB_OPERATIONS NEXT_EXPRESSION LAST_OPERATION
	| := ASSIGNING
	| eps

SUB_OPERATIONS ->
	MESSAGE MESSAGE_OPERATIONS L4_OPERATIONS
	| L4_OPERATION L4_OPERATIONS
	| eps

L4_OPERAND ->
	OBJECT MESSAGE_OPERATIONS

L4_OPERATION ->
	< L4_OPERAND
	| > CLOSING_OPERAND 

CLOSING_OPERAND ->
	L4_OPERAND
	| eps

L4_OPERATIONS ->
	L4_OPERATION L4_OPERATIONS
	| eps

MESSAGE_OPERATIONS ->
	MESSAGE MESSAGE_OPERATIONS
	| eps

LAST_OPERATION ->
	:= ASSIGNING
	| eps	

DISPATCH ->
	OBJECT

MESSAGE ->
	identifier MESSAGE_BODY

MESSAGE_BODY ->
	: MESSAGE_PARAMETER
	| OPENING_BRACKET MESSAGE_SUB_EXPR
	| eps

MESSAGE_SUB_EXPR ->
	EXPRESSION CLOSING_BRACKET

MESSAGE_PARAMETER ->
	OBJECT

OBJECT ->
	identifier
	| reference
	| member
	| global
	| OPENING_BRACKET SUB_EXPRESSION

IDLE_MESSAGE_PARAMETER ->
	CLOSING_BRACKET

PARAMETER ->
	identifier

TOKEN ->
	identifier TOKEN_BODY
	| member

TOKEN_PARAM ->
	identifier TOKEN_BODY

ATTRIBUTE ->
	attribute

BASE_CLASS ->
	identifier

TOKEN_BODY ->
	< TOKEN_PARAM TOKEN_PARAMS
	| eps

TOKEN_PARAMS ->
	, NEXT_TOKEN_PARAM
	| CLOSING_ANGLE_BRACKET

DECLARATION_END ->
	TERMINATOR
	| DECLARATION_END_EXPECTED

RET_STATEMENT_END ->
	TERMINATOR BLOCK_END
	| BLOCK_END

STATEMENT_END ->
	TERMINATOR
	| STATEMENT_END_EXPECTED

BLOCK_END ->
	]
	| BLOCK_CLOSING_EXPECTED

OPENING_CURLY_BRACKET ->
	{

OPENING_BRACKET ->
	(

OPEN_SBRACKET ->
	[

COLON ->
	:

DOUBLE_COLON ->
	::

ASSIGN_OPERATOR ->
	:=

TERMINATOR ->
	.

CLOSING_CURLY_BRACKET ->
	}

CLOSING_BRACKET ->
	)

CLOSING_ANGLE_BRACKET ->
	>
