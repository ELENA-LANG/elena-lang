__define START          	1
__define eps            	2

__define TEMPLATE               4111
__define SUBJECT_ARG		4112
__define EXPRESSION		4114
__define TOKEN_PARAM		4117
__define PARAMETER		4119
__define NESTED_CLASS		4120
__define OBJECT			4121
__define CODE_BLOCK		4122
__define MESSAGE		4123
__define MESSAGE_PARAMETER	4124
__define BLOCK_END		4125
__define L4_OPERATION           4127
__define DISPATCH               4128
__define ASSIGNING		4129
__define BASE_CLASS             4131
__define L3_OPERATION           4133
__define L7_OPERATION           4134
__define RET_STATEMENT		4135
__define L5_OPERATION           4136
__define EXTENSION		4139
__define RESEND                 4145
__define MESSAGE_REFERENCE	4147
__define ROOT_EXPRESSION        4152
__define L6_OPERATION           4161
__define TOKEN   		4172
__define SCOPE   		4173
__define ATTRIBUTE   		4174

__define DECLARATION_END_EXPECTED	16384
__define STATEMENT_END_EXPECTED         16385
__define BLOCK_CLOSING_EXPECTED         16386

__define eof            	12291
__define literal		12292
__define identifier		12293
__define private	        12294
__define reference		12295
__define integer		12296
__define hex			12297
__define real			12298
__define character		12299
__define long                   12300
__define wide                   12301
__define attribute		12303

START ->
	SCOPE_DECLARATION DECLARATIONS eof
	| ATTRIBUTE_DECLARATION DECLARATIONS eof
	| eof

SCOPE_DECLARATION ->
	TOKEN TOKENS SCOPE

ATTRIBUTE_DECLARATION ->
	ATTRIBUTE ATTRIBUTES TEMPLATE DECLARATION_END

DECLARATIONS ->
	SCOPE_DECLARATION DECLARATIONS
	| ATTRIBUTE_DECLARATION DECLARATIONS eof
	| eps
	
SCOPES ->
	TOKEN TOKENS SCOPE SCOPES
	| eps

NEXT_BASE_CLASS ->
	, BASE_CLASS NEXT_BASE_CLASS
	| eps

TOKENS ->
	TOKEN TOKENS
	| eps

ATTRIBUTES ->
	ATTRIBUTE ATTRIBUTES
	| eps

TOKEN_PARAMS ->
	, TOKEN_PARAM TOKEN_PARAMS
	| )

TOKEN ->
	identifier
	| private 

TEMPLATE ->
	identifier

NESTED_CLASS ->
	{ SCOPES CLOSING_CURLY_BRACKET

SCOPE ->
	= SYMBOL_EXPRESSION DECLARATION_END
	| { SCOPES CLOSING_CURLY_BRACKET
	| :: BASE_CLASS NEXT_BASE_CLASS SCOPE_BODY
	| : PARAMETER PARAMETER_BODY METHOD_BODY
	| => DISPATCH DECLARATION_END
	| <= RESEND
	| CODE_BLOCK
	| DECLARATION_END

SCOPE_BODY ->
	CLASS_BODY
	| = SYMBOL_EXPRESSION DECLARATION_END

PARAMETER_BODY ->
	: PARAMETER PARAMETER_BODY
	| SUBJECT_ARG : PARAMETER NEXT_PARAMETER
	| eps

NEXT_PARAMETER ->
	SUBJECT_ARG : PARAMETER NEXT_PARAMETER
	| eps

CLASS_BODY ->
	{ SCOPES CLOSING_CURLY_BRACKET
	| DECLARATION_END
	     
METHOD_BODY ->
	CODE_BLOCK
	| = RET_STATEMENT DECLARATION_END
	| => DISPATCH DECLARATION_END
	| <= RESEND
	
BASE_CLASS ->
	identifier TOKEN_BODY
	| reference 

TOKEN_BODY ->
	( TOKEN_PARAM TOKEN_PARAMS
	| eps

SYMBOL_EXPRESSION ->
	EXPRESSION

CODE_BLOCK ->
	[ STATEMENTS

STATEMENTS ->
	ROOT_EXPRESSION NEXT_STATEMENT
	| ^ RET_STATEMENT RET_STATEMENT_END
	| BLOCK_END

ROOT_EXPRESSION ->
	identifier ROOT_BODY
	| private ROOT_OPERATIONS
	| reference ROOT_OPERATIONS
	| ( SUB_EXPRESSION ROOT_SUBBODY

SUB_EXPRESSION ->
	EXPRESSION NEXT_SUB_EXPRESSION

NEXT_SUB_EXPRESSION ->
	CLOSING_BRACKET

NEXT_STATEMENT ->
	STATEMENT_END STATEMENTS
	| BLOCK_END

RET_STATEMENT ->
	EXPRESSION

SUB_EXPRESSION ->
	: PARAMETER CLOSURE_ARGS

CLOSURE_ARGS ->
	) CODE_BLOCK

EXPRESSION ->
	OBJECT OPERATIONS

DISPATCH ->
	OBJECT

RESEND ->
	MESSAGE RESEND_BODY

RESEND_BODY ->
	; CODE_BLOCK
	|  DECLARATION_END

MESSAGE_PARAMETER ->
	OBJECT

OBJECT ->
	identifier OBJECT_BODY
	| reference
	| private
	| integer
	| hex
	| long
	| real
	| literal
	| wide
	| character
	| % MESSAGE_REFERENCE
	| CODE_BLOCK	
	| ( SUB_EXPRESSION
	| NESTED_CLASS

OBJECT_BODY ->
	NESTED_CLASS
	| eps

MESSAGE ->
	identifier MESSAGE_BODY
	| private MESSAGE_BODY

MESSAGE_BODY ->
	SUBJECT_ARG : MESSAGE_PARAMETER NEXT_SUBJECT
	| : MESSAGE_PARAMETER MESSAGE_PARAMETERS NEXT_SUBJECT
	| eps

NEXT_SUBJECT ->
	SUBJECT_ARG : MESSAGE_PARAMETER SUBJECTS
	| eps

SUBJECTS ->
	SUBJECT_ARG : MESSAGE_PARAMETER SUBJECTS
	| eps

MESSAGE_PARAMETERS ->
	: MESSAGE_PARAMETER MESSAGE_PARAMETERS
	| eps

ROOT_BODY ->
	MESSAGE NEXT_ROOT_OPERATION
        | L5_OPERATION
        | L7_OPERATION
	| ASSIGNING
	| ( ROOT_SUB_EXPRESSION ROOT_CODE_BODY
	| eps

ROOT_SUB_EXPRESSION ->
	EXPRESSION CLOSING_BRACKET

NEXT_ROOT_OPERATION ->	
	L7_OPERATION
	| ASSIGNING
	| eps

ROOT_OPERATIONS ->
	MESSAGE NEXT_MESSAGE
	| L5_OPERATION
	| L7_OPERATION
	| ASSIGNING
	| eps

ROOT_SUBBODY ->
        L7_OPERATION
	| eps

ROOT_CODE_BODY ->
	CODE_BLOCK ROOT_CODE_BODY_OP
	| eps

ROOT_CODE_BODY_OP ->
        ; ROOT_CODE_BODY2
	| eps

OPERATIONS ->
	MESSAGE NEXT_MESSAGE L6_OPERATIONS L7_OPERATIONS
	| EXTENSION MESSAGE
	| L3_OPERATION
	| L4_OPERATION
	| L5_OPERATION
        | L6_OPERATION
        | L7_OPERATION
	| eps

NEXT_MESSAGE ->
	; MESSAGE NEXT_MESSAGE
	| eps

MESSAGE_OPERATIONS ->
	MESSAGE NEXT_MESSAGE
	| eps
	
L3_OPERATION ->
	* L3_OPERAND
	| / L3_OPERAND
	| && L3_OPERAND
	| ^^ L3_OPERAND

L3_OPERAND ->
	OBJECT MESSAGE_OPERATIONS

L4_OPERATION ->
	+ L4_OPERAND
	| - L4_OPERAND
	| &| L4_OPERAND

L4_OPERAND ->
	OBJECT MESSAGE_OPERATIONS

L5_OPERATION ->
	<< L5_OPERAND
	| >> L5_OPERAND

L5_OPERAND ->
	OBJECT

L6_OPERATIONS ->
	L6_OPERATION L6_OPERATIONS
	| eps	

L6_OPERATION ->
	== L6_OPERAND
	| != L6_OPERAND
	| < L6_OPERAND
	| > L6_OPERAND
	| <= L6_OPERAND
	| >= L6_OPERAND

L6_OPERAND ->
	OBJECT MESSAGE_OPERATIONS 

L7_OPERATION ->
	? L7_OPERAND ELSE_BODY
	| ! L7_OPERAND

L7_OPERATIONS ->
	L7_OPERATION
	| eps	
	
ELSE_OPERATION ->
	! L7_OPERAND
	
ELSE_BODY ->
	ELSE_OPERATION
	| eps	

L7_OPERAND ->
	CODE_BLOCK

EXTENSION ->
	:: OBJECT

ASSIGNING ->
	:= EXPRESSION

ATTRIBUTE ->
	attribute

TOKEN_PARAM ->
	identifier

SUBJECT_ARG ->
	identifier

PARAMETER ->
	identifier

MESSAGE_REFERENCE ->
	identifier
	| literal

TERMINATOR ->
	.

BLOCK_END ->
	]
	| BLOCK_CLOSING_EXPECTED

CLOSING_BRACKET ->
	)

CLOSING_CURLY_BRACKET ->
	}

DECLARATION_END ->
	TERMINATOR
	| DECLARATION_END_EXPECTED

STATEMENT_END ->
	TERMINATOR
	| STATEMENT_END_EXPECTED

RET_STATEMENT_END ->
	TERMINATOR BLOCK_END
	| BLOCK_END
