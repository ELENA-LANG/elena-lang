__define START          	1
__define eps            	2

__define TOKEN 			4112
__define GLOBAL_TOKEN 		4113
__define SCOPE_DECLARATION	4128
__define ATTRIBUTE_DECLARATION  4144
__define SIZE 			4160
__define BASE 			4176
__define BLOCK 			4192
__define DISPATCH		4208
__define TEMPLATE_ARGS		4272
__define NESTED_CLASS		4288
__define PARAMETER 		4304
__define EMPTY_PARAMETER	4320
__define BLOCK_END	       69776
__define EXPRESSION	      364592
__define ROOT_EXPRESSION      364593
__define OPERAND_EXPRESSION   364594
__define RET_EXPRESSION	      364848
__define MESSAGE	      135296
__define IMPLICIT_MESSAGE     135297
__define ASSIGN_OPERATOR      135328
__define L5_OPERATOR  	      135408

__define eof            	8208
__define identifier 		8209
__define hex 			8210
__define reference		8211
__define global			8212

START ->
	DECLARATION DECLARATIONS eof
	| eof

ATTRIBUTE_DECLARATION ->
	identifier SIZE

DECLARATIONS ->
	DECLARATION DECLARATIONS
	| eps

DECLARATION ->
	TOKENS SCOPE_DECLARATION
	| #define ATTRIBUTE_DECLARATION

SCOPE_DECLARATIONS ->
	TOKENS SCOPE_DECLARATION SCOPE_DECLARATIONS
	| eps

SCOPE_DECLARATION ->
	CLASS_DECLARATION
	| COLON BASE BASE_DECLARATION
	| SYMBOL_DECLARATION DECLARATION_END
	| METHOD_DECLARATION
	| TEMPLATE_ARGS TEMPLATE_DECLARATION
	| DECLARATION_END

TEMPLATE_DECLARATION ->
	CLASS_DECLARATION
	| COLON BASE BASE_DECLARATION
	| DECLARATION_END

BASE_DECLARATION ->
	CLASS_DECLARATION
	| DECLARATION_END

CLASS_DECLARATION ->
	{ SCOPE_DECLARATIONS }

METHOD_DECLARATION ->
	( PARAMETERS ) METHOD_BODY

METHOD_BODY ->
	BLOCK
	| = RET_EXPRESSION DECLARATION_END
	| => DISPATCH DECLARATION_END

DISPATCH ->
	EXPRESSION

SYMBOL_DECLARATION ->
	= EXPRESSION

RET_EXPRESSION ->
	EXPRESSION

BLOCK ->
	OPENING_CURLY_BRACKET STATEMENTS

STATEMENTS ->
	STATEMENT NEXT_STATEMENT
	| ^ RET_EXPRESSION RET_STATEMENT_END
	| BLOCK_END

NEXT_STATEMENT ->
        DECLARATION_END STATEMENTS
	| BLOCK_END

STATEMENT ->
	ROOT_EXPRESSION

ROOT_EXPRESSION ->
        TOKEN TR_OPERATIONS

EXPRESSION ->
	TOKEN T_OPERATIONS

OPERAND_EXPRESSION ->
	TOKEN

TR_OPERATIONS ->
	TOKEN T_OPERATIONS
	| TEMPLATE_ARGS TEMPLATE_OPERATIONS
	| IMPLICIT_OPERATION
	| ASSIGNMENT_OPERATION
	| OPERATIONS

T_OPERATIONS ->
	TOKEN T_OPERATIONS
	| TEMPLATE_ARGS TEMPLATE_OPERATIONS
	| IMPLICIT_OPERATION
	| :: DCOLON_OPERATION
	| ASSIGNMENT_OPERATION
	| OPERATIONS

OPERATIONS ->
	M_OPERATION 
	| L5_OPERATION
	| eps	

TEMPLATE_OPERATIONS ->
        IMPLICIT_OPERATION

M_OPERATION ->
	. MESSAGE MESSAGE_BODY

MESSAGE_BODY ->
	( MESSAGE_PARAMETERS )
	| EMPTY_PARAMETER

MESSAGE_PARAMETERS ->
	EXPRESSION NEXT_MESSAGE_PARAMETER
	| eps

NEXT_MESSAGE_PARAMETER ->
	, EXPRESSION NEXT_MESSAGE_PARAMETER
	| eps	

IMPLICIT_OPERATION ->
	IMPLICIT_MESSAGE MESSAGE_PARAMETERS )

ASSIGNMENT_OPERATION ->
	ASSIGN_OPERATOR EXPRESSION

DCOLON_OPERATION ->
	NESTED_CLASS

NESTED_CLASS ->
	OPENING_CURLY_BRACKET SCOPE_DECLARATIONS CLOSING_CURLY_BRACKET

L5_OPERATIONS ->
	L5_OPERATION
	| eps

ELSE_OPERATIONS ->
	: L5_OPERAND
	| eps	

L5_OPERATION ->
	L5_OPERATOR L5_OPERAND ELSE_OPERATIONS

L5_OPERAND ->
	BLOCK
	| OPERAND_EXPRESSION

PARAMETERS ->
	PARAMETER NEXT_PARAMETER	
	| eps

PARAMETER ->
	TOKENS

EMPTY_PARAMETER ->
	eps

NEXT_PARAMETER ->
	, PARAMETER NEXT_PARAMETER
	| eps
	
TOKENS ->
	TOKEN NEXT_TOKEN

NEXT_TOKEN ->
	TOKENS
	| eps

TOKEN ->
	identifier
	| reference

GLOBAL_TOKEN ->
	global

TEMPLATE_ARG ->
	TOKEN TOKEN_BODY
	| GLOBAL_TOKEN TOKEN_BODY	

TOKEN_BODY ->
	TEMPLATE_ARGS
	| eps	

TEMPLATE_ARGS ->
	OPENING_EDGE_BRACKET TEMPLATE_ARG NEXT_TEMPLATE_ARG

NEXT_TEMPLATE_ARG ->
	, TEMPLATE_ARG NEXT_TEMPLATE_ARG
	| >

MESSAGE ->
	identifier

ASSIGN_OPERATOR ->
	:=

L5_OPERATOR ->
	?
	| !

SIZE ->
	hex
	| integer

BASE ->
	TOKEN
	| GLOBAL_TOKEN

DYNAMIC_SIZES ->
	DYNAMIC_SIZE DYNAMIC_SIZES
	| eps
	
DYNAMIC_SIZE ->
	[]

IMPLICIT_MESSAGE ->
	OPENING_BRACKET

OPENING_BRACKET ->
	(

OPENING_CURLY_BRACKET ->
	{

OPENING_EDGE_BRACKET ->
	<

COLON ->
	:

RET_STATEMENT_END ->
	DECLARATION_END BLOCK_END
	| BLOCK_END

DECLARATION_END ->
	;

CLOSING_CURLY_BRACKET ->
	}

BLOCK_END ->
	}
