__define START          	1
__define eps            	2

__define SUBJECT_ARG		4112
__define EXPRESSION		4114
__define TOKEN_PARAM		4117
__define PARAMETER		4119
__define OBJECT			4121
__define BLOCK		        4122
__define MESSAGE		4123
__define MESSAGE_PARAMETER	4124
__define BLOCK_END		4125
__define L2_OPERATION           4127
__define DISPATCH               4128
__define ASSIGNING		4129
__define BASE_CLASS             4131
__define L1_OPERATION           4133
__define RET_STATEMENT		4135
__define L4_OPERATION           4161
__define TOKEN   		4172
__define DECLARATION   		4173
__define ATTRIBUTE   		4174
__define ARRAY_OPERATION        4179
__define IDLE_MESSAGE_PARAMETER 4189

__define eof            	12291
__define identifier		12293
__define private	        12294
__define reference		12295
__define integer		12296
__define hex			12297
__define attribute		12303
__define member			12304
__define global			12305

__define DECLARATION_END_EXPECTED	16384
__define STATEMENT_END_EXPECTED         16385
__define BLOCK_CLOSING_EXPECTED         16386

START ->
	TOKEN TOKENS DECLARATION DECLARATIONS eof
	| eof

DECLARATION ->
	EXPRESSION_DECLARATION DECLARATION_END
	| DISPATCH_DECLARATION DECLARATION_END	
	| CLASS_DECLARATION
	| DOUBLE_COLON BASE_CLASS NEXT_BASE_CLASS CLASS_DECLARATION
	| BLOCK
	| COLON PARAMETER PARAMETERS METHOD_BODY
	| OPENING_BRACKET SIGNATURE_BODY METHOD_BODY
	| ASSIGN_OPERATOR ASSIGNING DECLARATION_END
	| DECLARATION_END
 
DECLARATIONS ->
	TOKEN TOKENS DECLARATION DECLARATIONS
	| eps

TOKENS ->
	TOKEN TOKENS
	| eps

PARAMETERS ->
	TOKEN COLON PARAMETER PARAMETERS
	| eps

NEXT_BASE_CLASS ->
	, BASE_CLASS NEXT_BASE_CLASS
	| eps
	        
CLASS_DECLARATION ->
	OPENING_CURLY_BRACKET DECLARATIONS CLOSING_CURLY_BRACKET

SIGNATURE_BODY ->
	TOKEN_PARAM PARAMETER NEXT_SIGNATURE

NEXT_SIGNATURE_BODY ->
	SUBJECT_ARG OPENING_BRACKET SIGNATURE_BODY
	| eps

NEXT_SIGNATURE ->
	, TOKEN_PARAM PARAMETER NEXT_SIGNATURE
	| CLOSING_BRACKET NEXT_SIGNATURE_BODY

METHOD_BODY ->
	BLOCK	
	| = EXPRESSION DECLARATION_END
	| => DISPATCH DECLARATION_END

DISPATCH_DECLARATION ->
	=> DISPATCH 

EXPRESSION_DECLARATION ->
	= EXPRESSION_BODY

EXPRESSION_BODY ->
	EXPRESSION
	| ATTRIBUTE

BLOCK ->
	OPEN_SBRACKET STATEMENTS

STATEMENTS ->
	^ RET_STATEMENT RET_STATEMENT_END
	| EXPRESSION NEXT_STATEMENT
	| BLOCK_END

NEXT_STATEMENT ->
	STATEMENT_END STATEMENTS
	| BLOCK_END

RET_STATEMENT ->
 	EXPRESSION

ASSIGNING ->
	EXPRESSION

SUB_EXPRESSION ->
	EXPRESSION CLOSING_BRACKET
	| IDLE_MESSAGE_PARAMETER

NEXT_EXPRESSION ->
	, OBJECT SUB_OPERATIONS NEXT_EXPRESSION
	| eps	

EXPRESSION ->
	OBJECT OPERATIONS

OPERATIONS ->
	MESSAGE NEXT_MESSAGE L1_OPERATIONS L2_OPERATIONS L4_OPERATIONS NEXT_EXPRESSION LAST_OPERATION
	| ARRAY_OPERATION ARRAY_OPERATIONS ARRAY_LAST_OPERATIONS
	| L1_OPERATION L1_OPERATIONS L2_OPERATIONS L4_OPERATIONS NEXT_EXPRESSION LAST_OPERATION
	| L2_OPERATION L2_OPERATIONS L4_OPERATIONS NEXT_EXPRESSION LAST_OPERATION
	| L4_OPERATION L4_OPERATIONS NEXT_EXPRESSION LAST_OPERATION
	| , OBJECT SUB_OPERATIONS NEXT_EXPRESSION LAST_OPERATION
	| := ASSIGNING
	| eps

LAST_OPERATION ->
	:= ASSIGNING
	| eps	

ARRAY_LAST_OPERATIONS ->
	MESSAGE NEXT_MESSAGE L1_OPERATIONS L2_OPERATIONS L4_OPERATIONS NEXT_EXPRESSION
	| L1_OPERATION L1_OPERATIONS L2_OPERATIONS L4_OPERATIONS NEXT_EXPRESSION
	| L2_OPERATION L2_OPERATIONS L4_OPERATIONS NEXT_EXPRESSION
	| L4_OPERATION L4_OPERATIONS NEXT_EXPRESSION
	| := ASSIGNING
	| eps

SUB_OPERATIONS ->
	MESSAGE NEXT_MESSAGE L1_OPERATIONS L2_OPERATIONS L4_OPERATIONS
	| ARRAY_OPERATION ARRAY_OPERATIONS
	| L1_OPERATION L1_OPERATIONS L2_OPERATIONS L4_OPERATIONS
	| L2_OPERATION L2_OPERATIONS L4_OPERATIONS
	| L4_OPERATION L4_OPERATIONS
	| eps

L1_OPERAND ->
	OBJECT ARRAY_OPERATIONS MESSAGE_OPERATIONS

L1_OPERATIONS ->
	L1_OPERATION L1_OPERATIONS
	| eps

L1_OPERATION ->
	* L1_OPERAND
	| / L1_OPERAND

L2_OPERAND ->
	OBJECT ARRAY_OPERATIONS MESSAGE_OPERATIONS L1_OPERATIONS

L2_OPERATIONS ->
	L2_OPERATION L2_OPERATIONS
	| eps
	
L2_OPERATION ->
	+ L2_OPERAND
	| - L2_OPERAND

L4_OPERAND ->
	OBJECT ARRAY_OPERATIONS MESSAGE_OPERATIONS L1_OPERATIONS L2_OPERATIONS
                
L4_OPERATION ->
	< L4_OPERAND
	| > CLOSING_OPERAND 

CLOSING_OPERAND ->
	L4_OPERAND
	| eps

L4_OPERATIONS ->
	L4_OPERATION L4_OPERATIONS
	| eps

MESSAGE_OPERATIONS ->
	MESSAGE NEXT_MESSAGE
	| eps

ARRAY_OPERATION ->
	[ EXPRESSION CLOSING_SBRACKET

ARRAY_OPERATIONS ->
	ARRAY_OPERATION ARRAY_OPERATIONS
	| eps

DISPATCH ->
	OBJECT

MESSAGE ->
	identifier MESSAGE_BODY

NEXT_MESSAGE ->
	; MESSAGE NEXT_MESSAGE
	| eps

MESSAGE_BODY ->
	: MESSAGE_PARAMETER NEXT_SUBJECT_ARG
	| OPENING_BRACKET MESSAGE_SUB_EXPR
	| eps
	          
NEXT_SUBJECT_ARG ->
	SUBJECT_ARG : MESSAGE_PARAMETER NEXT_SUBJECT_ARG
	| eps

MESSAGE_SUB_EXPR ->
	EXPRESSION CLOSING_BRACKET

MESSAGE_PARAMETER ->
	OBJECT

OBJECT ->
	identifier
	| reference
	| member
	| global
	| private
	| integer
	| hex
	| OPENING_BRACKET SUB_EXPRESSION

IDLE_MESSAGE_PARAMETER ->
	CLOSING_BRACKET

PARAMETER ->
	identifier

TOKEN ->
	identifier TOKEN_BODY
	| member

TOKEN_PARAM ->
	identifier TOKEN_BODY
	| reference
	| global
	| integer

ATTRIBUTE ->
	attribute

BASE_CLASS ->
	identifier TOKEN_BODY
	| reference TOKEN_BODY
	| global TOKEN_BODY

SUBJECT_ARG ->
	identifier

TOKEN_BODY ->
	< NEXT_TOKEN_PARAM
	| eps

TOKEN_PARAMS ->
	, NEXT_TOKEN_PARAM
	| CLOSING_ANGLE_BRACKET

NEXT_TOKEN_PARAM ->
	TOKEN_PARAM TOKEN_PARAMS

DECLARATION_END ->
	TERMINATOR
	| DECLARATION_END_EXPECTED

RET_STATEMENT_END ->
	TERMINATOR BLOCK_END
	| BLOCK_END

STATEMENT_END ->
	TERMINATOR
	| STATEMENT_END_EXPECTED

BLOCK_END ->
	]
	| BLOCK_CLOSING_EXPECTED

OPENING_CURLY_BRACKET ->
	{

OPENING_BRACKET ->
	(

OPEN_SBRACKET ->
	[

COLON ->
	:

DOUBLE_COLON ->
	::

ASSIGN_OPERATOR ->
	:=

TERMINATOR ->
	.

CLOSING_CURLY_BRACKET ->
	}

CLOSING_SBRACKET ->
	]

CLOSING_BRACKET ->
	)

CLOSING_ANGLE_BRACKET ->
	>
