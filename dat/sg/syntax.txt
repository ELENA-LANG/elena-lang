__define START          	1
__define eps            	2

__define TOKEN			4112
__define ROOT_EXPRESSION        4113
__define EXPRESSION             4114
__define DECLARATION            4115
__define PARAMETER		4119
__define NESTED_CLASS		4120
__define BLOCK		        4122
__define MESSAGE		4123
__define BLOCK_END		4125
__define DISPATCH               4128
__define ASSIGN_OPERATOR	4129
__define BASE                   4131
__define ATTRIBUTE_DECLARATION  4174
__define RET_EXPRESSION         4198
__define IMPLICIT_MESSAGE       4199
__define SIZE			4200
__define DYNAMIC_SIZE		4201

__define eof            	12291
__define literal		12292
__define identifier		12293
__define reference		12295
__define integer		12296
__define attribute		12303

START ->
	ATTRIBUTE_DECLARATION ATTRIBUTE_DECLARATIONS DECLARATIONS eof
	| TOKEN TOKENS DECLARATION DECLARATIONS eof
	| eof

ATTRIBUTE_DECLARATIONS ->
	ATTRIBUTE_DECLARATION ATTRIBUTE_DECLARATIONS
	| eps

DECLARATIONS ->
	TOKEN TOKENS DECLARATION DECLARATIONS
	| eps

DECLARATION ->
	SYMBOL_DECLARATION DECLARATION_END
	| CLASS_DECLARATION
	| COLON BASE CLASS_DECLARATION
	| METHOD_DECLARATION
	| DECLARATION_END
	| SIZE_DECLARATION DECLARATION_END

CLASS_DECLARATION ->
	{ DECLARATIONS }

SYMBOL_DECLARATION ->
	= EXPRESSION

METHOD_DECLARATION ->
	( PARAMETERS ) METHOD_BODY

PARAMETERS ->
	PARAMETER NEXT_PARAMETER
	| eps

NEXT_PARAMETER ->
	, PARAMETER NEXT_PARAMETER
	| eps

PARAMETER ->
	TOKEN TOKENS

METHOD_BODY ->
	BLOCK
	| => DISPATCH DECLARATION_END
	| = RET_EXPRESSION DECLARATION_END

RET_EXPRESSION ->
	EXPRESSION

BLOCK ->
	OPENING_CURLY_BRACKET STATEMENTS

STATEMENTS ->
	STATEMENT NEXT_STATEMENT
	| BLOCK_END

NEXT_STATEMENT ->
        DECLARATION_END STATEMENTS
	| BLOCK_END

DISPATCH ->
	EXPRESSION

STATEMENT ->
	ROOT_EXPRESSION

ROOT_EXPRESSION ->
	TOKEN TOKENS OPERATIONS
	| NESTED_CLASS SUB_OPERATIONS
	| integer SUB_OPERATIONS
	| literal SUB_OPERATIONS

EXPRESSION ->
	identifier EXPR_OPERATIONS
	| reference EXPR_OPERATIONS
	| integer SUB_OPERATIONS
	| literal SUB_OPERATIONS
	| NESTED_CLASS SUB_OPERATIONS

SUB_OPERATIONS ->
	. MESSAGE MESSAGE_BODY OPERATIONS
	| IMPLICIT_OPERATION OPERATIONS
	| L1_OPERATION L1_OPERATIONS
	| L2_OPERATION L2_OPERATIONS
	| L3_OPERATION L3_OPERATIONS
	| ARRAY_OPERATION ARRAY_OPERATIONS
	| eps

EXPR_OPERATIONS ->
	TOKEN OPERATIONS
	| . MESSAGE MESSAGE_BODY OPERATIONS
	| ASSIGN_OPERATOR EXPRESSION
	| IMPLICIT_OPERATION OPERATIONS
	| L1_OPERATION L1_OPERATIONS
	| L2_OPERATION L2_OPERATIONS
	| L3_OPERATION L3_OPERATIONS
	| ARRAY_OPERATION ARRAY_OPERATIONS
	| eps

OPERATIONS ->
	. MESSAGE MESSAGE_BODY OPERATIONS
	| ASSIGN_OPERATOR EXPRESSION
	| IMPLICIT_OPERATION OPERATIONS
	| L1_OPERATION L1_OPERATIONS
	| L2_OPERATION L2_OPERATIONS
	| ARRAY_OPERATION ARRAY_OPERATIONS
	| eps

ARRAY_OPERATIONS ->
	[ ARRAY_OPERATION ARRAY_OPERATIONS
	| eps

ARRAY_OPERATION ->
	[ EXPRESSION CLOSING_SBRACKET

L1_OPERATIONS ->
	L1_OPERATION L1_OPERATIONS
	| eps

L1_OPERATION ->
	* L1_OPERAND
	| / L1_OPERAND 

L1_OPERAND ->
	OBJECT

L2_OPERATIONS ->
	L2_OPERATION L2_OPERATIONS
	| eps

L2_OPERATION ->
	+ L2_OPERAND
	| - L2_OPERAND 

L2_OPERAND ->
	OBJECT

L3_OPERATIONS ->
	L3_OPERATION L3_OPERATIONS
	| eps

L3_OPERATION ->
	< L3_OPERAND
	| > L3_OPERAND 

L3_OPERAND ->
	OBJECT

IMPLICIT_OPERATION ->
	IMPLICIT_MESSAGE MESSAGE_PARAMETERS )

NESTED_CLASS ->
	OPENING_CURLY_BRACKET DECLARATIONS CLOSING_CURLY_BRACKET

TOKENS ->
	TOKEN TOKENS
	| eps

ASSIGNING ->
	EXPRESSION

MESSAGE ->
	identifier

MESSAGE_BODY ->
	( MESSAGE_PARAMETERS )

MESSAGE_PARAMETERS ->
	EXPRESSION NEXT_MESSAGE_PARAMETER
	| eps

NEXT_MESSAGE_PARAMETER ->
	, EXPRESSION NEXT_MESSAGE_PARAMETER
	| eps

BASE ->
	TOKEN

TOKEN_BODY ->
	< TOKEN NEXT_TOKEN_PARAM
	| DYNAMIC_SIZE
	| eps

NEXT_TOKEN_PARAM ->
	, TOKEN NEXT_TOKEN_PARAM
	| > DYNAMIC_SIZE_OPTION

DYNAMIC_SIZE_OPTION ->
	DYNAMIC_SIZE
	| eps

TOKEN ->
	identifier TOKEN_BODY

ATTRIBUTE_DECLARATION ->
	attribute ATTRIBUTE_BODY

ATTRIBUTE_BODY ->
	= TOKEN DECLARATION_END

DYNAMIC_SIZE ->
	[]

SIZE_DECLARATION ->
	[ SIZE ]

SIZE ->
	integer

DECLARATION_END ->
	;

OPENING_CURLY_BRACKET ->
	{

BLOCK_END ->
	}

CLOSING_CURLY_BRACKET ->
	}

IMPLICIT_MESSAGE ->
	(

ASSIGN_OPERATOR ->
	:=

COLON ->
	:

CLOSING_SBRACKET ->
	]
