__define START          	 1
__define eps            	 2

// NOTE : low word should be unique for every key
__define SCOPE_DECLARATION	 4128
__define BLOCK 		         4192
__define MESSAGE	         4224
__define BLOCK_END	         4240
__define PARAMETER 		 4304
__define FIELD_ASSIGNER          4384
__define TOKEN_ARGS_END       4199184
__define MESSAGE_OP_END       4395568
__define ASSIGNMENT_OP_END    4395776
__define TOKEN 	              8392720
__define SCOPE_TOKEN 	      8392721
__define EXPRESSION           8589840
__define ROOT_EXPRESSION      8589841

__define eof            	 8208
__define identifier 	 	 8209
__define hex 			 8210
__define reference		 8211
__define global			 8212
__define integer                 8213
__define literal                 8214
__define meta                    8215
__define wide                    8216
__define character               8217
__define long                    8218
__define real                    8219
__define constant                8220

START ->
	DECLARATION DECLARATIONS eof
	| eof

DECLARATION ->
        SCOPE_TOKEN NEXT_DECLARATION_ITEM

DECLARATIONS ->
	DECLARATION DECLARATIONS
	| eps

NEXT_DECLARATION_ITEM ->
	SCOPE_TOKEN NEXT_DECLARATION_ITEM
	| SCOPE_DECLARATION

SCOPE_DECLARATION ->
        CLASS_DECLARATION
	| SYMBOL_DECLARATION DECLARATION_END
	| METHOD_DECLARATION
	| ASSIGN_OPERATOR FIELD_ASSIGNER DECLARATION_END
	| DECLARATION_END

CLASS_DECLARATION ->
	{ SCOPE_DECLARATIONS }

SYMBOL_DECLARATION ->
	= EXPRESSION

METHOD_DECLARATION ->
	( PARAMETERS ) METHOD_BODY

FIELD_ASSIGNER ->
	EXPRESSION

SCOPE_DECLARATIONS ->
	SCOPE_TOKEN NEXT_DECLARATION_ITEM SCOPE_DECLARATIONS
	| eps

METHOD_BODY ->
	BLOCK

BLOCK ->
	OPENING_CURLY_BRACKET STATEMENTS

STATEMENTS ->
	STATEMENT NEXT_STATEMENT
	| BLOCK_END

NEXT_STATEMENT ->
        DECLARATION_END STATEMENTS
	| BLOCK_END

STATEMENT ->
	ROOT_EXPRESSION

ROOT_EXPRESSION ->
	TOKEN TR_OPERATIONS
	| integer E_OPERATIONS
	| long E_OPERATIONS
	| real E_OPERATIONS
	| hex E_OPERATIONS
	| literal E_OPERATIONS
	| character E_OPERATIONS
	| wide E_OPERATIONS
	| constant E_OPERATIONS

EXPRESSION ->
        TOKEN TI_OPERATIONS
	| integer E_OPERATIONS
	| long E_OPERATIONS
	| real E_OPERATIONS
	| hex E_OPERATIONS
	| literal E_OPERATIONS
	| character E_OPERATIONS
	| wide E_OPERATIONS
	| constant E_OPERATIONS

TOKENS ->
	TOKEN NEXT_TOKEN

NEXT_TOKEN ->
	TOKENS
	| eps

TOKEN ->
	identifier
	| reference

SCOPE_TOKEN ->
	identifier TOKEN_ARGS
	| reference TOKEN_ARGS

TOKEN_ARGS ->
	OPENING_EDGE_BRACKET TOKEN_ARG NEXT_TOKEN_ARG TOKEN_ARGS_END
	| eps

TOKEN_ARG ->
	SCOPE_TOKEN

NEXT_TOKEN_ARG ->
	, TOKEN_ARG NEXT_TOKEN_ARG

TOKEN_ARGS_END ->
	CLOSING_EDGE_BRACKET

TR_OPERATIONS ->
	TOKEN T_OPERATIONS
	| T0_OPERATIONS

TI_OPERATIONS ->
	TOKEN T_OPERATIONS
	| L1_OPERATION L1_OPERATIONS
	| L2_OPERATION L2_OPERATIONS
	| ASSIGNMENT_OPERATION
	| OPERATIONS

T_OPERATIONS ->
	TOKEN T_OPERATIONS
	| L1_OPERATION L1_OPERATIONS L2_OPERATIONS
        | T0_OPERATIONS

T0_OPERATIONS ->
	L2_OPERATION L2_OPERATIONS 
	| ASSIGNMENT_OPERATION

E_OPERATIONS ->
        L1_OPERATION L1_OPERATIONS L2_OPERATIONS
	| L2_OPERATION L2_OPERATIONS
	| OPERATIONS

OPERATIONS ->
	eps

L1_OPERATIONS ->
	L1_OPERATION L1_OPERATIONS
	| eps

L1_OPERATION ->
  	IMPLICIT_OPERATION

L2_OPERATIONS ->
	L2_OPERATION L2_OPERATIONS
	| eps

L2_OPERATION ->
	. L2_OPERATOR_BODY

L2_OPERATOR_BODY ->
	MESSAGE MESSAGE_BODY

IMPLICIT_OPERATION ->
        OPENING_BRACKET MESSAGE_PARAMETERS MESSAGE_OP_END

MESSAGE_BODY ->
	OPENING_BRACKET MESSAGE_PARAMETERS MESSAGE_OP_END

MESSAGE_OP_END ->
	CLOSING_BRACKET 

PARAMETERS ->
	PARAMETER NEXT_PARAMETER	
	| eps

NEXT_PARAMETER ->
	, PARAMETER NEXT_PARAMETER
	| eps	

MESSAGE_PARAMETERS ->
	EXPRESSION NEXT_MESSAGE_PARAMETER
	| eps

NEXT_MESSAGE_PARAMETER ->
	, EXPRESSION NEXT_MESSAGE_PARAMETER
	| eps	

MESSAGE ->
	identifier

PARAMETER ->
	TOKENS	

ASSIGNMENT_OPERATION ->
	ASSIGN_OPERATOR EXPRESSION ASSIGNMENT_OP_END

ASSIGNMENT_OP_END ->
	eps

ASSIGN_OPERATOR ->
	:=

OPENING_BRACKET ->
	(

OPENING_CURLY_BRACKET ->
	{

OPENING_EDGE_BRACKET ->
	<

DECLARATION_END ->
	;

CLOSING_EDGE_BRACKET ->
	>

CLOSING_BRACKET ->
	)

BLOCK_END ->
	}
