__define eof                 8195;
__define identifier         12292;
__define integer            12293;
__define string             12294;
__define hexinteger         12295;
__define reference          12296;
__defineterminal }          12297;

__define DECLARATION         4096;
__define BLOCK               7184;
__define NO_BODY             7185;
__define PARAMETER 	     4106;
__define END_OF_BLOCK        4107;
__define DICTIONARY          4128;
__define META_STATEMENT      4129;
__define INCLUDE_STATEMENT   4130;
__define RELOAD_STATEMENT    4131;
__define MESSAGE             4176;
__define EXPRESSION          6192;
__define OBJECT              4145;
__define TEMPLATE_TYPE       4146;
__define ARRAY_TYPE          4147;
__define NESTED              4224;
__define CLOSURE             4225;
__define RET_EXPRESSION      7220;
__define NESTED_EXPRESSION   6197;
__define CLOSURE_EXPRESSION  6198;
__define INDEXER_OPERATION   6209;
__define ASSIGN_OPERATION    6210;
__define ADDITION_ASSIGNMENT 6211;
__define MESSAGE_OPERATION   6225;
__define PROPERTY_OPERATION  6228;
__define POSTFIX             4192;
__define TEMPLATE_POSTFIX    4193;
__define TEMPLATE_ARG        4208;
__define DIMENSION           5233;

START ::= 
        NEXT_DECLARATION;

NEXT_DECLARATION ::=
        DECLARATION NEXT_DECLARATION
        | "#new" DICTIONARY NEXT_DECLARATION
        | "#let" META_STATEMENT NEXT_DECLARATION
        | "#include" INCLUDE_STATEMENT NEXT_DECLARATION
	| "#reload" RELOAD_STATEMENT NEXT_DECLARATION
        | eof;

SUB_DECLARATIONS ::=
        DECLARATION SUB_DECLARATIONS
        | "#new" DICTIONARY SUB_DECLARATIONS
        | "#let" META_STATEMENT SUB_DECLARATIONS
        | "#include" INCLUDE_STATEMENT SUB_DECLARATIONS
	| eps;	

DECLARATION ::=
        identifier ISCOPE_R;

ISCOPE_R ::=
        identifier ISCOPE_R
	| reference ISCOPE_R
	| TEMPLATE_BRACKETS SCOPE_OPT
	| DYNAMIC_DIMENSION ^ARRAY_TYPE DDSCOPE_R
        | SCOPE_OPT;

DDSCOPE_R ::=
	identifier SCOPE_OPT;

DICTIONARY ::=
        identifier IDICTIONARY_R DECLARATION_END;

IDICTIONARY_R ::=
        identifier IDICTIONARY_R
        | eps;

SCOPE ::=
        "=" RET_EXPRESSION DECLARATION_END
	| "{" SUB_DECLARATIONS END_OF_BLOCK
	| "(" PARAMETERS ")" METHOD_BODY_OPT
	| "[" DIMENSION "]" DECLARATION_END
        | DECLARATION_END;

SCOPE_OPT ::=
	SCOPE
	| ":" POSTFIX POSTFIXES SCOPE;

POSTFIXES ::=
	"," POSTFIX POSTFIXES
	| eps;

POSTFIX ::=
	identifier IPOSTFIX_R;

IPOSTFIX_R ::=
	TEMPLATE_BRACKETS ^ TEMPLATE_POSTFIX
	| eps;

METHOD_BODY_OPT ::=
	METHOD_BODY
	| ":" POSTFIX POSTFIXES METHOD_BODY;

METHOD_BODY ::=
	BLOCK
	| "=" RET_EXPRESSION DECLARATION_END
	| NO_BODY;

NO_BODY ::=
	";";

BLOCK ::= 
	"{" STATEMENTS END_OF_BLOCK;

STATEMENTS ::=
	ROOT_EXPRESSION NEXT_STATEMENT
	| "^" RET_EXPRESSION NEXT_LAST_STATEMENT
	| "#include" INCLUDE_STATEMENT STATEMENTS
	| eps;

NEXT_STATEMENT ::=
	DECLARATION_END STATEMENTS
	| eps;

NEXT_LAST_STATEMENT ::=
	DECLARATION_END
	| eps;

INCLUDE_STATEMENT ::=
	INCLUDE_ARG INCLUDE_ARGS DECLARATION_END;

RELOAD_STATEMENT ::=
	identifier DECLARATION_END;

INCLUDE_ARGS ::=
	INCLUDE_ARG INCLUDE_ARGS
	| eps;

INCLUDE_ARG ::=
	identifier
	| reference;

META_STATEMENT ::=
        EXPRESSION DECLARATION_END;

ROOT_EXPRESSION ::=
	EXPRESSION;

RET_EXPRESSION ::=
	EXPRESSION;

MESSAGE_PARAMETER ::=
	EXPRESSION;

EXPRESSION ::=
        identifier I_R
	| reference I_R
        | integer ^OBJECT
        | hexinteger ^OBJECT
        | string ^OBJECT
	| CLOSURE_EXPRESSION ^CLOSURE;

CLOSURE_EXPRESSION ::=
	"{" STATEMENTS "}";

NESTED_EXPRESSION ::=
	"{" SUB_DECLARATIONS "}";

I_R ::=
        identifier I_R
	| NESTED_EXPRESSION ^NESTED N_R
        | I_L0_R
	| I_L1_R
	| I_L2_R
	| I_L3_R NEXT_L3_R
        | eps ^OBJECT; 

N_R ::=
	eps;

I_L0_R ::=
        "[" ^OBJECT EXPRESSION "]" ^INDEXER_OPERATION INDEXER_R
        | "+=" ^OBJECT EXPRESSION ^ADDITION_ASSIGNMENT;

I_L1_R ::=
        ":=" ^OBJECT EXPRESSION ^ASSIGN_OPERATION;

L1_R ::=
        ":=" EXPRESSION ^ASSIGN_OPERATION;

I_L2_R ::=
	"(" ^OBJECT MESSAGE_PARAMETERS ")" ^ MESSAGE_OPERATION;

I_L3_R ::=
	"." ^OBJECT MESSAGE MESSAGE_R;

NEXT_L3_R ::=
	"." MESSAGE MESSAGE_R NEXT_L3_R
	| eps;

INDEXER_R ::=
        L1_R
        | eps;

MESSAGE_R ::=
	"(" MESSAGE_PARAMETERS ")" ^ MESSAGE_OPERATION
	| eps ^PROPERTY_OPERATION;

MESSAGE_PARAMETERS ::=
	MESSAGE_PARAMETER NEXT_MESSAGE_PARAMETER
	| eps;

NEXT_MESSAGE_PARAMETER ::=
	"," MESSAGE_PARAMETER NEXT_MESSAGE_PARAMETER
	| eps;

PARAMETERS ::=
	PARAMETER NEXT_PARAMETER
	| eps;

NEXT_PARAMETER ::= 
	"," PARAMETER NEXT_PARAMETER
	| eps;

PARAMETER ::=
	identifier PARAMETER_R;

PARAMETER_R ::=
	identifier PARAMETER_R
	| eps;

DIMENSION ::=
        integer
        | hexinteger;	

DYNAMIC_DIMENSION ::=
	"[]";

TEMPLATE_ARG ::=
	identifier
	| reference;

MESSAGE ::=
	identifier;

TEMPLATE_BRACKETS ::=
	"<" TEMPLATE_ARG TEMPLATE_ARGS;

TEMPLATE_ARGS ::=
	"," TEMPLATE_ARG TEMPLATE_ARGS
	|  ">";

ADDITION_ASSIGNMENT ::=
        "+=";

DECLARATION_END ::=
	";";

END_OF_BLOCK ::=
	"}";
